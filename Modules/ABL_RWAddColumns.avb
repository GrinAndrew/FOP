'#include "ABL_ADO.avb"
'#include "ABL_Common.avb"
'#include "ABL_const.avb"

Option Explicit

'---------------------------------------------------------------------------------------------------------------
' ***                  REP_ID - ОТЧЁТ   ***   ITEM - АНАЛИТИКА   ***   ATTR - ПАРАМЕТРЫ                      ***
' ***                                                                                                        ***
' ***              aRepStdAttrL - Стандартные   ***   aRepItemAttr - Дополнительные (из REP_ITEMS_ATTR)      ***
' ***                                                                                                        ***
'---------------------------------------------------------------------------------------------------------------
Sub ABL_RWAddColumns
	Dim i, SQL, aData, SQL_Rep, aData_Rep
	Dim RepID, RepKind 	' ID отчета ВЫБРАННОГО пользователем
	Dim RW, Book			' Объект RepWizard и его Книга(Excel)
	Dim aRepStdAttrL		' Массив по каждому уровню отчета с кол-вом уже отображенных стандартных параметров
	Dim WS

	If Not com_GetLogicParamValue(Workarea, prmDBExtRepWizard, False) Then Exit Sub

	RepID = WorkArea.Map("RepID")

	Set RW = WorkArea.CreateReport("RepWizard")
	RW.Load RepID

	RepKind = WorkArea.Map("RepKind")
	If RepKind <> 100 Then Exit Sub	

	Set WS = Workarea.Site
	RW.KindID = WS.ID

	If RW.AccID = 0 Then
		If WS.kind = acAccount Then
			RW.AccID = WS.ID
		Else
			MsgBox "Не установлен счет для мастера отчетов", vbExclamation, rw.caption
			Exit Sub
		End If
	End If

	Workarea.Map("Result") = False

	SQL_Rep = "Select REP_ITEM, REP_ITEM_ATTR_NAME From REP_ITEMS_ATTR Where Rep_ID = " & RepID

	If GetRs2(SQL_Rep, aData_Rep) Then
	
		Workarea.Map("Result") = True

		If RW.Build Then

			Set Book = RW.MakeSheet
			Book.Map("RepID") = RepID
			Book.Sheet1.PageSetup.Orientation = 2
			LoadStdAttr RepID, aRepStdAttrL

			For i = 1 To RW.Levels
				' Есть ли в нашей таблице настроек что-то для этого отчёта и этого уровня?
				SQL = "Select REP_ITEM, REP_ITEM_ATTR_NAME From REP_ITEMS_ATTR Where Rep_ID = " & RepID & " and REP_ITEM = " & RW.LevelMode(i)
				If	GetRs2(SQL, aData) Then
					AddLevelAttributes RW, Book, i, aRepStdAttrL, aData
				End If
			Next
	
			Book.Sheet1.AutoFit 2
			SetCodeForSheet Book, RW

			UnMergeCells Book.Sheet1, 4
			DoMergeCells Book.Sheet1, 4
			Book.Sheet1.Range(1, 4, Book.Sheet1.columns, 5).SetBorder acBrdGrid,,RGB(192, 192, 192)
		End If

	End If

End Sub

'---------------------------------------------------------------------------------------------------------------
' *** Процедура подсчёта кол-ва УЖЕ выведенных в отчет СТАНДАРТНЫХ параметров по КАЖДОМУ УРОВНЮ              ***
'---------------------------------------------------------------------------------------------------------------
Sub LoadStdAttr(RepID, aRepStdAttrL)
	Dim i, j, SQL, aData		' Строка со стандартными параметрами для этого отчета
	Dim aRepStdAttr 				' Массив колонок со стандартными параметрами для этого отчета
	Dim StdAttrCount				' Количество отображенных стандартных параметров в уровне
	Dim RepStdAttr					' Одна колонка

	SQL = "Select Rep_Ps6 From REPORTS Where Rep_ID = " & RepID
	If	GetRs2(SQL, aData) Then 

		aRepStdAttrL = Split(aData(0, 0), Chr(10))
		For i = 0 To UBound(aRepStdAttrL)

			If aRepStdAttrL(i) <> "" Then

				aRepStdAttr = Split(aRepStdAttrL(i), Chr(13))
				StdAttrCount = 0
				For j = 0 To UBound(aRepStdAttr)
					RepStdAttr = aRepStdAttr(j)
					If CharAt(RepStdAttr, 1) = "-" Then	StdAttrCount = StdAttrCount + 1
				Next

			Else
				StdAttrCount = 1
			End If

			aRepStdAttrL(i) = StdAttrCount
		Next
	End If
End Sub

'---------------------------------------------------------------------------------------------------------------
' *** Процедура вставки и заполнения дополнительных параметров для заданного УРОВНЯ                          ***
' *** (присутствующих в нашей таблице настройки)                                                             ***
'---------------------------------------------------------------------------------------------------------------
Sub AddLevelAttributes(RW, Book, Level, aRepStdAttrL, aRepItemAttr)
	Dim i
	Dim ColOffSet ' Смещение за счет уже выведенных в отчете стандартных параметров до начала нашего уровня

	ColOffSet = 0
	For i = 0 To Level - 1
		ColOffSet = ColOffSet + aRepStdAttrL(i)
	Next
	
	CopyColumns		Book, Book.Sheet1, ColOffSet, UBound(aRepItemAttr, 2), aRepItemAttr
	aRepStdAttrL(Level) = aRepStdAttrL(Level) + UBound(aRepItemAttr, 2) + 1
	' Смещение этого уровня увеличится за счет добавленных НАМИ колонок для доп.параметров

	FindItemValue	RW, Book.Sheet1, RW.Root, 1, Level, 1, ColOffSet, aRepItemAttr
End Sub

'---------------------------------------------------------------------------------------------------------------
' *** Скопируем колонку с аналитикой столько раз, сколько параметров для него нам необходимо добавить        ***
' *** Это делается для того, чтобы формат новых колонок совпадал с форматом первоначальных колонок в отчете  ***
'---------------------------------------------------------------------------------------------------------------
Sub CopyColumns (Book, Sh, Col, Count, Name)
	Dim i, j

	For i = 0 To Count
		Sh.Activate
		Sh.Range(Col, 4, Col, Sh.Rows).Select 
		Book.Copy
		Sh.InsertColumn(Col+i+1)
		Sh.Cell(4,Col+i+1).Select
		Book.Paste
		Sh.Range(Col+i+1,4,Col+i+1,5).MergeCells False
		Sh.Cell(4,Col+i+1).Value = ""
		Sh.Cell(5,Col+i+1).Value = Name(1, i)
		For j = 6 To Sh.Rows
			Sh.Cell(j,Col+i+1).Value = ""
		Next
	Next
End Sub

'---------------------------------------------------------------------------------------------------------------
' *** Процедура поиска ЗНАЧЕНИЯ каждой выведенной в отчете АНАЛИТИКИ                                         ***
'---------------------------------------------------------------------------------------------------------------
Sub FindItemValue (RW, Sh, Root, ByVal Level, ItemLevel, ByRef RowNo, ColOffSet, aRepItemAttr)
	Dim i

	For i = 1 To Root.Children
		If i > 1 Then
			RowNo = RowNo + 1
		End If

		If Level = ItemLevel Then
			InsertAttrValue Sh, Root.Child(i).Value, RW.LevelMode(Level), RowNo, ColOffSet + 1, aRepItemAttr
		End If

		If Root.Child(i).Children > 0 Then
			FindItemValue RW, Sh, Root.Child(i), Level+1, ItemLevel, RowNo, ColOffSet, aRepItemAttr
		End If
	Next
End Sub

'---------------------------------------------------------------------------------------------------------------
' *** Процедура вставки значений дополнительно необходимых параметров                                        ***
'---------------------------------------------------------------------------------------------------------------
Sub InsertAttrValue (Sh, Id, Item, RowNo, Column, aItemAttr)
	Dim i, StdRows, Elem

	If Id <> 0 Then
		
		For i = 0 To UBound(aItemAttr, 2)
			Select	Case Item
				Case 3 
					Set Elem = Workarea.Agent(Id)
				Case 4 
					Set Elem = Workarea.Agent(Id)
				Case 5 
					Set Elem = Workarea.Entity(Id)
				Case 9 
					Set Elem = Workarea.Misc(Id)
			End Select
			Sh.Cell(RowNo+6,Column).Value = GetAttrValue(Elem, aItemAttr(1, i))
			Column = Column + 1 	
		Next

	Else 
		Sh.Cell(RowNo+6,Column).Value = ""
	End If
End Sub

Function GetAttrValue(Elem, AttrName)
	Dim AG_PARAM_STD
	' Ориентировано на ABL_RWAddColumns.adg :
	Set AG_PARAM_STD = CreateLibObject("Map")
	AG_PARAM_STD("Контактное лицо (Станд.пар-р)") = "Contact"
	AG_PARAM_STD("Дата приема на работу (Станд.пар-р)") = "DateIn"
	AG_PARAM_STD("Дата увольнения (Станд.пар-р)") = "DateOut"
	AG_PARAM_STD("Номер телефона (Станд.пар-р)") = "Phone"
	AG_PARAM_STD("Регистрационный номер (Станд.пар-р)") = "RegNo"
	AG_PARAM_STD("Адрес почтовый") = ""
	AG_PARAM_STD("Адрес юридический") = ""


	If InStr(AttrName,"(Станд.пар-р)") > 0 Then
		Execute "GetAttrValue = Elem." & AG_PARAM_STD(AttrName)
	ElseIf AttrName = "Адрес почтовый" Then
		GetAttrValue = com_addressnormalize(com_getparamvalue(Elem, prmAgDeliveryAddr, "<Не указан>"))
	ElseIf AttrName = "Адрес юридический" Then
		GetAttrValue = com_addressnormalize(Elem.Address)
	Else
		GetAttrValue = Com_GetParamValueRef(Elem, AttrName, "<Не указано>")
	End If
End Function

'--------------------------------------------------------------------'
' Связь между типом аналитики в отчете и её типом в объектной модели '
'--------------------------------------------------------------------'
'| RepWizard              | LevelMode | Accent   | Const            |'
'|------------------------|-----------|----------|------------------|'
'| Корреспондент1         | 3         | acAgent  | 3                |'
'| Корреспондент2         | 4         | acAgent  | 3                |'
'| Объект учёта           | 5         | acEntity | 4                |'
'| Произвольная аналитика | 9         | acMisc   | 5                |'
'--------------------------------------------------------------------'

Sub SetCodeForSheet(Book, RW)
	Dim Text

	Text = 				"	Sub ShtBook_OnPeriodClick(Buttons) " & vbNewLine & _
				"		If Workarea.Period.Browse Then " & vbNewLine & _
				"			Workarea.Map.Item(""RepID"") = Map(""RepID"") " & vbNewLine & _
				"			App.RunModule ""ABL_RWAddColumns.avb"", ""ABL_RWAddColumns"" " & vbNewLine & _
				"			Close " & vbNewLine & _
				"		End If " & vbNewLine & _
				"	End Sub" & vbNewLine & _
				"	Sub ShtBook_OnLoad" & vbNewLine & _
				"		Dim btn" & vbNewLine & _
				"		ToolBar.Visible = True" & vbNewLine & _
				"		Set btn = ToolBar.Create(1)" & vbNewLine & _
				"		btn.Tag = ""Refresh""" & vbNewLine & _
				"		btn.Image = 89" & vbNewLine & _
				"		btn.caption = ""Обновить""" & vbNewLine & _
				"	End Sub" & vbNewLine & _
				"	Sub ShtBook_OnBarClick(Command)" & vbNewLine & _
				"		ExecBarTag Command" & vbNewLine & _
				"	End Sub" & vbNewLine & _
				"	Sub Refresh" & vbNewLine & _
				"		Workarea.Map.Item(""RepID"") = Map(""RepID"") " & vbNewLine & _
				"		App.RunModule ""ABL_RWAddColumns.avb"", ""ABL_RWAddColumns""" & vbNewLine & _
				"		Close " & vbNewLine & _
				"	End Sub"

	Book.SetScript Text, 1
End Sub
'---
'	процедура формирует шапку
'---
Sub DoMergeCells(Sh, RowNo)
	Dim j, k

	For j = 1 To Sh.Columns
		If Not Sh.Cell(RowNo, j).MergeCells Then
			If (j < Sh.Columns) Then
				If Sh.Cell(RowNo, j + 1).Value <> "" Then
					If Sh.Cell(RowNo + 1, j).Value = "" Then
						Sh.Range(j, RowNo, j, RowNo + 1).MergeCells = True
					End If
				Else
					If Sh.Cell(RowNo + 1, j).Value = "" Then Sh.Cell(RowNo + 1, j).Value = Sh.Cell(RowNo, j).Value

					For k = j + 1 To Sh.Columns
						If Sh.Cell(RowNo, k).Value <> "" Then	Exit For
					Next
	
					k = k - 1
					Sh.Range(j, RowNo, k, RowNo).MergeCells = True
					j = k
				End If
			End If
		End If
	Next
	
End Sub
'---
'	процедура сбрасывает объединенные ячейки
'---
Sub UnMergeCells(sh, RowNo)
	Dim i

	For i = 1 To Sh.Columns
		If Sh.Cell(RowNo, i).MergeCells Then Sh.Cell(RowNo, i).MergeCells = False
		Sh.Cell(RowNo, i).BackColor = sh.PArent.StdBackColor(1)
		Sh.Cell(RowNo + 2, i).BackColor = sh.PArent.StdBackColor(2)
	Next

End Sub
