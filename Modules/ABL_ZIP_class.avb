Option Explicit

Class zip
	Private FldZip, Shell
	Public FSO, RD
	Private zFldPath

'---
'
'---
	Private Sub Class_initialize	
		Set FldZip = Nothing
		Set Shell = CreateObject("Shell.Application")
		Set FSO = CreateObject("Scripting.FileSystemObject")
		Set RD = CreateLibObject("Redirect")
	End Sub
'---
'
'---
	Private Sub Class_terminate
		Set FldZip = Nothing
		Set Shell = Nothing
		Set FSO = Nothing
		Set RD = Nothing
	End Sub
'---
'
'---
	Public Function Open(ZipFldPath)
		Dim ZipFile, Result

		Result = 0

		If Not IsFileExists(ZipFldPath) Then
			Result = 1
			Set ZipFile = CreateTextFile(ZipFldPath)

			If Not ZipFile Is Nothing Then 
				ZipFile.Write Chr(80) & Chr(75) & Chr(5) & Chr(6) & String(18, 0)
				ZipFile.Close
			Else
				Open = -1
				Exit Function
			End If

			Set ZipFile = Nothing

			Sleep1
		Else
			ZipFldPath = RD.GetFullPath(ZipFldPath)
			Result = 2
		End If

		zFldPath = ZipFldPath
		Set FldZip = Shell.NameSpace(ZipFldPath)

		If FldZip Is Nothing Then Result = -1

		Open = Result
	End Function
'---
'
'---
	Function AddFolder(FolderPath, FileFilter)
		Dim Fld

		If Not FldZip Is Nothing And FolderPath <> "" Then
			Set Fld = Shell.Namespace(FolderPath).items()

			If FileFilter <> "" Then Fld.Filter 32, FileFilter
		
			If Not Fld Is Nothing Then
				FldZip.CopyHere Fld, 4 + 8 + 16
			End If

		End If
	End Function
'---
'
'---
	Function AddFile(FilePath)
		Dim zipFile, zipFileName, srcFile, Pos, ZipFileSize

		If Not FldZip Is Nothing And FilePath <> "" Then
			FldZip.CopyHere FilePath

			Pos = InStrRev(FilePath, "\") 

			If Pos <> 0 Then
				zipFileName = Mid(FilePath, Pos + 1)
				Set srcFile = Shell.NameSpace(Left(FilePath, Pos)).ParseName(zipFileName)

				Do 
					Sleep1
					Set zipFile = FldZip.ParseName(zipFileName)
					If Not zipFile Is Nothing Then
						ZipFileSize = ZipFile.Size
					Else
						ZipFileSize = 0
						AddFile = False
						Exit Function
					End If
						
				Loop Until zipFilesize = srcFile.size
			End If
		End If
	End Function
'---
'
'---
	Function UnzipFiles(FldDistPath, Flags)
		Dim FldDist

		UnzipFiles = False

		If Not FldZip Is Nothing And FldDistPath <> "" Then
			Set FldDist = Shell.NameSpace(FldDistPath)

			If Not FldDist Is Nothing Then
				FldDist.CopyHere FldZip.Items, Flags
				UnzipFiles = True
			End If
		End If

	End Function
'---
'
'---
	Function UnzipFile(FldDistPath, UnzipFileName, Flags)
		Dim FldDist, UnzipFilePath

		UnzipFile = False

		If Not FldZip Is Nothing And FldDistPath <> "" And UnzipFileName <> "" Then
			Set FldDist = Shell.NameSpace(FldDistPath)

			If Not FldDist Is Nothing Then
				If Right(FldDistPath, 1) <> "\" Then FldDistPath = FldDistPath & "\"
				UnzipFilePath = FldDistPath & UnzipFileName
				If FSO.FileExists(UnzipFilePath) Then FSO.DeleteFile UnzipFilePath, True

				FldDist.CopyHere FldZip.ParseName(UnzipFileName).Path, Flags
				UnzipFile = True
			End If
		End If

	End Function
'---
'
'---
	Private Sub Sleep1
		Dim t	
		t = Timer
		Do
			'oevent
		Loop Until ( Timer - t ) > 0.5
	End Sub
'---
'
'---
	Public Sub Close
		If Not FldZip Is Nothing Then
'			FldZip.Close
			While Not ReleaseZipFile()
				Sleep1
			Wend

			Set FldZip = Nothing
		End If
	End Sub
'---
'
'---
	Private Function ReleaseZipFile()
		Dim f

		ReleaseZipFile = False
		On Error Resume Next
		Set f = FSO.OpenTextFile(zFldPath, 8)

		ReleaseZipFile = (Err.number = 0)

	End Function
'---
'
'---
	Public Property Get GetTempFolder(FldName, AlwaysCreate)
		Dim TmlFolder, FldZipPath

		Set TmlFolder = FSO.GetSpecialFolder(2)
		FldZipPath = TmlFolder.Path
		If Right(FldZipPath, 1) <> "\" Then FldZipPath = FldZipPath & "\"
		FldZipPath = FldZipPath & FldName

		If Not FSO.FolderExists(FldZipPath) Then
			If Not AlwaysCreate Then
				FldZipPath = ""
			Else
				On Error Resume Next
				Set TmlFolder = FSO.CreateFolder(FldZipPath)

				If Err.Number <> 0 Then
					MsgBox Err.Description & " (#" & Err.Number & ")", vbExclamation, "Установка приложений/обновлений"
					FldZipPath = ""
				End If
			End If
		End If

		GetTempFolder = FldZipPath

	End Property
'---
'
'---
	Public Function GetFullPath(ByVal fName, ByVal fExt)
		Dim aPath, fPath

		fName = Trim(fName)
		If fExt = "" Then fExt = "*" & Mid(fName, InStrRev(fName, "."))
		aPath = RD.GetPathArray(fExt)

		If UBound(aPath) <> -1 Then
			fPath = Trim(aPath(0))
			GetFullPath = fPath & iif(Right(fPath, 1) <> "\", "\", "") & fName
		Else
			GetFullPath = ""
		End If
	End Function

	Private Function CreateTextFile(ZipFldPath)
		On Error Resume Next 
		Set CreateTextFile = FSO.CreateTextFile(ZipFldPath)

		If Err.number <> 0 Then 
			MsgBox "Ошибка создания файла " + ZipFldPath + vbCrLf + "#" + CStr(Err.Number) + " " + Err.Description, _
				vbCritical, _
				"Создание архива"

			Err.Clear
			Set CreateTextFile = Nothing
		End If
	End Function

End Class

'---
'
'---
'Sub InstallFiles(UpdateZIP, BackupZIP)
'	Dim z, FldZipPath, Fl, FldSrc, FlPath, ZIPPath
'
'	Set z = New zip
'	FldZipPath = z.GetTempFolder(z.FSO.GetTempName, True)
'
'	If FldZipPath <> "" Then
'		ZIPPath = z.GetFullPath(UpdateZIP, "")
'
'		If ZIPPath <> "" Then
'			If z.open(ZIPPath) > 0 Then
'				If z.UnzipFiles(FldZipPath, 0) Then
'					z.Close
'					ZIPPath = z.GetFullPath(BackupZIP, "*.backup")
'
'					If ZIPPath <> "" Then
'						If z.open(ZIPPath) > 0 Then
'							Set FldSrc = z.FSO.GetFolder(FldZipPath)
'	
'							For Each Fl In FldSrc.Files
'								FlPath = z.GetFullPath(Fl.Name, "")
'	
'								If FlPath = "" Then
'									MsgBox "no path for " & Fl.Name
'									'writelog Array("Ошибка:", "Для файла не указан путь в настройке приложения", Fl.Name), updater
'									z.FSO.DeleteFolder FldZipPath
'									Exit Sub
'								End If
'
'								If IsFileExists(FlPath) Then z.AddFile FlPath
'							Next
'
'							z.close
'
'							For Each Fl In FldSrc.Files
'								FlPath = z.GetFullPath(Fl.Name, "")
'								If IsFileExists(FlPath) Then z.FSO.DeleteFile FlPath, True
'								z.FSO.CopyFile fl.Path, flPath, True
'							Next
'
'							z.FSO.DeleteFolder FldZipPath
'						Else
'							MsgBox "Error open ZIP for " & ZIPPath
'						End If
'					Else
'						MsgBox "no path for " & BackupZIP
'					End If
'				Else
'					MsgBox "Error unzip for " & FldZipPath
'				End If
'			Else
'				MsgBox "Error open ZIP for " & ZIPPath
'			End If
'		Else
'			MsgBox "no path for " & UpdateZIP
'		End If
'	Else
'		MsgBox "Error creating temp folder" & FldZipPath
'	End If
'End Sub
'---
'
'---
